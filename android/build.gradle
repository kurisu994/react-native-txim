// Top-level build file where you can add configuration options common to all sub-projects/modules.
buildscript {
    if (rootProject.buildDir == project.buildDir) {
        repositories {
            maven { url "https://mirrors.tencent.com/repository/maven/tencent_public"}
            maven { url "https://mirrors.tencent.com/repository/maven/liteavsdk" }
            maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
            google()
            mavenCentral()
        }
        dependencies {
            classpath 'com.android.tools.build:gradle:3.5.4'
            classpath 'com.google.gms:google-services:4.2.0'
            classpath 'com.huawei.agconnect:agcp:1.3.1.300'
            classpath 'com.kezong:fat-aar:1.2.17'
        }
    }
}

def getExtOrDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : project.properties['RNTxim_' + name]
}

def getExtOrIntegerDefault(name) {
    return rootProject.ext.has(name) ? rootProject.ext.get(name) : (project.properties['RNTxim_' + name]).toInteger()
}

apply plugin: 'com.android.library'


android {
    compileSdkVersion getExtOrIntegerDefault('compileSdkVersion')
    buildToolsVersion getExtOrDefault('buildToolsVersion')

    defaultConfig {
        minSdkVersion getExtOrIntegerDefault('minSdkVersion')
        targetSdkVersion getExtOrIntegerDefault('targetSdkVersion')
        manifestPlaceholders = [
                IM_APPID        : getExtOrDefault('ImAppId'),
                //小米
                XM_PUSH_APPKEY: getExtOrDefault('XmAppkey'),
                XM_PUSH_APPID: getExtOrDefault('XmAppId'),
                XM_PUSH_BUZID: getExtOrIntegerDefault('XmPushBuzId'),
                //华为
                HW_PUSH_BUZID: getExtOrIntegerDefault('HwPushBuzId'),
                HW_PUSH_APPID: getExtOrDefault('HwAppId'),
                //魅族
                MZ_PUSH_BUZID: getExtOrIntegerDefault('MzPushBuzId'),
                MZ_PUSH_APPID: getExtOrDefault('MzAppId'),
                MZ_PUSH_APPKEY: getExtOrDefault('MzAppKey'),
                //vivo
                VIVO_PUSH_BUZID: getExtOrIntegerDefault('VivoPushBuzId'),
                VIVO_PUSH_APPID: getExtOrDefault('VivoAppId'),
                VIVO_PUSH_APPKEY: getExtOrDefault('VivoAppKey')

        ]
    }

    repositories {
        maven { url "https://mirrors.tencent.com/repository/maven/tencent_public"}
        maven { url "https://mirrors.tencent.com/repository/maven/liteavsdk" }
        maven { url "https://mirrors.tencent.com/nexus/repository/maven-public/" }
        google()
        mavenCentral()
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation 'com.facebook.react:react-native:+'
    implementation 'androidx.multidex:multidex:2.0.1'
    implementation 'androidx.appcompat:appcompat:1.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'
    // txim
    implementation 'com.tencent.imsdk:imsdk:5.6.+'
    implementation 'com.tencent.imsdk:imsdk-plus:5.6.+'
    implementation 'com.google.code.gson:gson:2.8.5'
    // google firebase
    implementation('com.google.firebase:firebase-core:17.0.0')
    // google 云消息传递
    implementation ('com.google.firebase:firebase-messaging:19.0.1')
    // tpns推送
    // 小米
    implementation "com.tencent.tpns:xiaomi:1.2.1.2-release"
    // 魅族
    implementation "com.tencent.tpns:meizu:1.2.1.2-release"
    // OPPO
    implementation "com.tencent.tpns:oppo:1.2.1.2-release"
    // vivo
    implementation "com.tencent.tpns:vivo:1.2.1.2-release"
    // 华为
    implementation 'com.tencent.tpns:huawei:1.2.1.3-release'
    implementation 'com.huawei.hms:push:5.0.2.300'
}
